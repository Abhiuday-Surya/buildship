import org.gradle.util.GradleVersion

apply plugin: eclipsebuild.TestBundlePlugin
apply plugin: 'groovy'

dependencies {
    bundled 'org.spockframework:spock-core:1.3-groovy-2.5'
    implementation project(':org.eclipse.buildship.compat')
    api project(':org.eclipse.buildship.core')
}

eclipseTest {
    fragmentHost 'org.eclipse.buildship.core'
    applicationName 'org.eclipse.pde.junit.runtime.coretestapplication'
    optionsFile rootProject.project(':org.eclipse.buildship.core').file('.options')
}

def compatibilityCheckClassName = 'org.eclipse.buildship.core.internal.workspace.ImportingProjectCrossVersionTest'
def compatibilityCheckDescriptorPrefix = 'Can import a simple project build with Gradle '
def minimumGradleVersion;

tasks.getByName("crossVersionEclipseTest") {
    afterTest { TestDescriptor descriptor, TestResult result ->
        if (descriptor.className == compatibilityCheckClassName &&
                descriptor.name.startsWith(compatibilityCheckDescriptorPrefix) &&
                result.resultType == TestResult.ResultType.SUCCESS) {
            def testingGradleVersion = descriptor.name.substring(compatibilityCheckDescriptorPrefix.length())
            if (minimumGradleVersion == null || (GradleVersion.version(testingGradleVersion) < GradleVersion.version(minimumGradleVersion))) {
                minimumGradleVersion = testingGradleVersion;
            }
        }
    }
}

task updateProperties {
    doLast {
        if (project.hasProperty('eclipse.test.java.version')) {
            def javaProperty = project.property('eclipse.test.java.version').toString()
            if (javaProperty != null) {
                def propertiesFile = file('../org.eclipse.buildship.core/src/main/resources/org/eclipse/buildship/core/internal/gradle/java-minimum-supported-toolingAPI.properties')
                Properties properties = new Properties()
                new FileInputStream(propertiesFile).withCloseable { res ->
                    properties.load(res)
                }
                if (properties.get(javaProperty) == null && minimumGradleVersion != null) {
                    properties.put(javaProperty, minimumGradleVersion)
                    new FileOutputStream(propertiesFile).withCloseable { res ->
                        properties.store(res, null)
                    }
                }
            }
        }
    }
}

tasks.getByName("crossVersionEclipseTest").finalizedBy(updateProperties)
